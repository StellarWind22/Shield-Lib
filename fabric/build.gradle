plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven { url "https://maven.terraformersmc.com/releases/"}
    maven {url = "https://api.modrinth.com/maven"}
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"
    include "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    //Mod Menu
    modCompileOnly modRuntimeOnly ("com.terraformersmc:modmenu:${project.mod_menu_version}"), {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //Midnight lib
    modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}-fabric"
    include "maven.modrinth:midnightlib:${project.midnightlib_version}-fabric"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

tasks.register("extractRemappedJar", Copy) {
    dependsOn("remapJar")

    from(zipTree(remapJar.archiveFile))
    into(layout.buildDirectory.dir("tmp/remapped"))

    exclude 'data/**'
    exclude 'assets/**'
}

tasks.register("jarProduction", Jar) {
    group = "build"
    description = "Build production JAR for Fabric using stripped common resources"

    dependsOn(":common:jarProduction", "extractRemappedJar")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Include extracted remapped content
    from(layout.buildDirectory.dir("tmp/remapped"))

    // Include stripped resources
    from(project(':common').file('tmp')) {
        include 'data/**'
        include 'assets/**'
    }

    doLast {
        delete(layout.buildDirectory.dir("tmp/remapped"))
    }
}
