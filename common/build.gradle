import groovy.json.JsonSlurper
import groovy.json.JsonOutput

plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    common rootProject.enabled_platforms.split(',')
}

repositories {
    maven {url = "https://api.modrinth.com/maven"}
}

dependencies {
    // We depend on Fabric Loader here to use the Fabric @Environment annotations,
    // which get remapped to the correct annotations on each platform.
    // Do NOT use other classes from Fabric Loader.
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "dev.architectury:architectury:$rootProject.architectury_api_version"

    //DO NOT USE ANY FABRIC SPECIFIC CLASSES!!!
    modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}-fabric"
}

tasks.register("stripDebugResources") {
    group = "build"
    description = "Remove debug entries and files for production build"

    def tmpDir = file("$projectDir/tmp")

    doFirst {
        def rootDir = file("$projectDir/src/main/resources/data")
        tmpDir.mkdirs()

        copy {
            from "src/main/resources"
            into tmpDir
        }

        def tagDirs = [
                "minecraft/tags/item",
                "shieldlib/tags/item",
                "minecraft/tags/item/enchantable",
                "shieldlib/tags/item/enchantable",
                "c/tags/item/tools"
        ]

        def entriesToRemove = [
                "shieldlib:shield",
                "shieldlib:component_shield",
                "shieldlib:tower_shield",
                "shieldlib:buckler_shield",
                "shieldlib:heater_shield",
                "shieldlib:spiked_tower_shield",
                "shieldlib:spiked_buckler_shield",
                "shieldlib:spiked_heater_shield"
        ]

        def debugDirs = [
                "shieldlib/enchantment"
        ]

        def debugFiles = [
                "recovery.json",
                "reflect.json"
        ]

        // Process tag JSONs
        tagDirs.each { dir ->
            fileTree(new File(tmpDir, dir)).matching { include "*.json" }.each { file ->
                def json = new JsonSlurper().parse(file)
                if (json.values instanceof List) {
                    json.values.removeAll { it in entriesToRemove }
                    file.text = JsonOutput.prettyPrint(JsonOutput.toJson(json))
                    println("Stripped debug items from: $file")
                }
            }
        }

        // Delete debug files
        debugDirs.each { dir ->
            def fullDir = new File(tmpDir, dir)
            if(fullDir.exists() && fullDir.isDirectory()) {
                debugFiles.each {debugFile ->
                    fullDir.eachFileMatch(debugFile) { file ->
                        file.delete()
                        print("Deleted file: $file from build.")
                    }
                }
            }
        }
    }

    doLast {
        tasks.named("jar") {
            from(tmpDir)
        }
    }
}


// Make the main build task depend on this if production
tasks.named("build") {
    dependsOn("stripDebugResources")
}




