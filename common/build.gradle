import groovy.json.JsonSlurper
import groovy.json.JsonOutput

architectury {
    common rootProject.enabled_platforms.split(',')
}

repositories {
    maven {url = "https://api.modrinth.com/maven"}
}

dependencies {
    // We depend on Fabric Loader here to use the Fabric @Environment annotations,
    // which get remapped to the correct annotations on each platform.
    // Do NOT use other classes from Fabric Loader.
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "dev.architectury:architectury:$rootProject.architectury_api_version"

    //DO NOT USE ANY FABRIC SPECIFIC CLASSES!!!
    modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}-fabric"
}

jar.doFirst {
    def tagDirs = [
            file("$buildDir/resources/main/data/shieldlib/tags/item"),
            file("$buildDir/resources/main/data/shieldlib/tags/item/enchantable"),
            file("$buildDir/resources/main/data/c/tags/item/tools"),
            file("$buildDir/resources/main/data/minecraft/tags/item/enchantable")
    ]

    def entriesToRemove = [
            'shieldlib:shield',
            'shieldlib:banner_shield',
            'shieldlib:buckler_shield'
    ]

    tagDirs.each { tagDir ->
        if (tagDir.exists()) {
            tagDir.eachFileMatch(~/.+\.json$/) { File jsonFile ->
                def json = new JsonSlurper().parse(jsonFile)

                if (json.values instanceof List) {
                    json.values.removeAll { it in entriesToRemove }

                    jsonFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(json))
                    System.out.println("Stripped debug items from: ${jsonFile}")
                }
            }
        }
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}
