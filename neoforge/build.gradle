plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {url = 'https://maven.neoforged.net/releases'}
    maven {url = "https://api.modrinth.com/maven"}
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"
    include "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    //Midnight lib
    modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}-neoforge"
    include "maven.modrinth:midnightlib:${project.midnightlib_version}-neoforge"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

tasks.register("extractRemappedJar", Copy) {
    dependsOn("remapJar")

    from(zipTree(remapJar.archiveFile))
    into(layout.buildDirectory.dir("tmp/remapped"))

    exclude 'data/**'
    exclude 'assets/**'
}

tasks.register("jarProduction", Jar) {
    group = "build"
    description = "Build production JAR for NeoForge using stripped common resources"

    dependsOn(":common:jarProduction", "extractRemappedJar")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Include extracted remapped content
    from(layout.buildDirectory.dir("tmp/remapped"))

    // Include stripped resources
    from(project(':common').file('tmp')) {
        include 'data/**'
        include 'assets/**'
    }

    doLast {
        delete(layout.buildDirectory.dir("tmp/remapped"))
    }
}
